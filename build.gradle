plugins {
    id 'application'
    id 'org.openjfx.javafxplugin' version '0.0.14'
}

repositories {
    mavenCentral()
}

sourceSets {
    main {
        java {
            srcDirs = ['VSEclipseExampleProject/robotic_arm_api_examples']
        }
    }
}
def osName = org.gradle.internal.os.OperatingSystem.current()
application {
    mainClass = 'cads.roboticArm.simulation.Server'
    applicationDefaultJvmArgs = [
            '-Dprism.verbose=true',
            '-Dprism.debug=true',
            '-Dprism.forceGPU=true',
            '-Djavafx.animation.fullspeed=true',
            '-XX:+UseG1GC',
            '-XX:+UseStringDeduplication'
    ]
    if (osName.isWindows()) {
        applicationDefaultJvmArgs += ['-Dprism.order=d3d,es2,sw']
    } else {
        applicationDefaultJvmArgs += ['-Dprism.order=es2,sw']
    }
}

javafx {
    version = '17.0.15'
    // 3D-Module hinzufügen
    modules = [ 'javafx.controls', 'javafx.fxml', 'javafx.graphics' ]
}



dependencies {
    implementation 'org.openjfx:javafx-controls:17.0.15'
    implementation 'org.openjfx:javafx-fxml:17.0.15'
    implementation 'org.openjfx:javafx-graphics:17.0.15'

    if (osName.isWindows()) {
        runtimeOnly 'org.openjfx:javafx-graphics:17.0.15:win'
    } else if (osName.isLinux()) {
        runtimeOnly 'org.openjfx:javafx-graphics:17.0.15:linux'
    } else if (osName.isMacOsX()) {
        runtimeOnly 'org.openjfx:javafx-graphics:17.0.15:mac'
    }

    implementation files('VSEclipseExampleProject/resources/libs/CaDSControlGUI.jar')
    implementation files('VSEclipseExampleProject/resources/libs/CaDSLogger.jar')
    implementation files('VSEclipseExampleProject/resources/libs/CaDSRoboticArmInterface.jar')

    implementation 'org.slf4j:slf4j-api:2.0.7'
    implementation 'org.slf4j:slf4j-simple:2.0.7'
    implementation 'com.google.code.gson:gson:2.10.1'
}


// Task für erweiterte Debugging-Informationen
tasks.register('runDebug', JavaExec) {
    group = 'application'
    description = 'Run with extended 3D debugging'
    classpath = sourceSets.main.runtimeClasspath
    mainClass = application.mainClass
    jvmArgs = [
            '-Dprism.verbose=true',
            '-Dprism.debug=true',
            '-Dprism.info=true',
            '-Dprism.showdirty=true',
            '-Dprism.printrendergraph=true',
            '-Dquantum.verbose=true',
            '-Djavafx.animation.fullspeed=true'
    ]
}